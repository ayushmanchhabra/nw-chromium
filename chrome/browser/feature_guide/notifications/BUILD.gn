# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
  import("//chrome/android/features/android_library_factory_tmpl.gni")
}

source_set("public") {
  sources = [
    "feature_notification_guide_service.cc",
    "feature_notification_guide_service.h",
    "feature_type.h",
  ]

  deps = [ "//skia" ]

  public_deps = [
    "//base",
    "//chrome/browser/notifications/scheduler/public",
    "//components/keyed_service/core",
  ]
}

if (is_android) {
  java_cpp_enum("jni_enums") {
    visibility = [ "*" ]

    sources = [ "feature_type.h" ]
  }

  android_library("java") {
    sources = [ "android/java/src/org/chromium/chrome/browser/feature_guide/notifications/FeatureNotificationGuideService.java" ]

    deps = [
      "//base:base_java",
      "//third_party/androidx:androidx_annotation_annotation_java",
    ]
    srcjar_deps = [ ":jni_enums" ]
  }

  android_library_factory("factory_java") {
    # These deps will be inherited by the resulting android_library target.
    deps = [
      ":java",
      "//base:base_java",
      "//chrome/browser/profiles/android:java",
    ]

    # This internal file will be replaced by a generated file so the resulting
    # android_library target does not actually depend on this internal file.
    sources = [ "internal/android/java/src/org/chromium/chrome/browser/feature_guide/notifications/FeatureNotificationGuideServiceFactory.java" ]
  }
}

group("unit_tests") {
  testonly = true

  deps = [ "internal:unit_tests" ]
}
